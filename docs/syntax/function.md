# 函数库
这篇文档将记录和解释所有的内置函数，同时提供一些使用思路。

### $变量 变量名 变量值$
设置和更改变量。
> 一般用来定义长变量名的变量。

### $取变量 变量名$
取用变量。
> 长变量名的变量只能这么取。

### $全局变量 变量名 变量值$
设置和更改全局变量。这个变量可以一直维持到程序结束。\
这种变量不能与前述的变量同名，否则报错。

### $随机数 起始值 结束值$
生成指定范围内的随机整数(包含起始值，但不包括结束值)。

### $JSON 添加 JSON名称 参数$
向指定JSON数组/对象中添加一个元素。

如果上述`参数`位置包含**一**个参数，则`JSON名称`处必须是一个**JSON数组**，该值会被添加到数组最后。\
如果上述`参数`位置包含**两**个参数，则`JSON名称`处必须是一个**JSON对象**，两个参数分别视为**键**和**值**。 \
如果出现上述类型不匹配的情况，将会报错。

由于JSON对象中键的唯一性，所以如果键名重复，则会覆盖原有的值。\
例如：
```NyaPlus
添加元素测试
a:["111","222"]
b:{"key1":"val1","key2":"val2"}
修改前的数组：\n
%a%\n
%b%\n\n
$JSON 添加 a 333$               ##预期：向数组 arr1 添加元素
$JSON 添加 b key3 val3$         ##预期：向对象 obj1 添加键值对
$JSON 添加 b key1 newval$       ##预期：覆盖原有的键key1的值
修改后的数组：\n
%a%\n
%b%
```
上述例子的输出是：
```
修改前的数组：
["111","222"]
{"key1":"val1","key2":"val2"}

修改后的数组：
["111","222","333"]
{"key1":"newval","key2":"val2","key3":"val3"}
```

### $JSON 删除 JSON名称 参数$
删除指定JSON数组/对象中的一个元素。\
此处`参数`位置只需一个参数，\
对于JSON数组，参数为要删除的元素的**索引**（即编号，从0开始）\
对于JSON对象，参数为要删除的元素的**键**。\
如果传入JSON数组，而后面传入的参数无法转化为一个整数，将会报错。\
简单示例：
```NyaPlus
删除元素测试
a:["111","222"]
b:{"key1":"val1","key2":"val2"}
$JSON 删除 a 1$             ##删除数组索引为1（第二个）的元素
$JSON 删除 b key1$          ##删除对象键名为 key1 的元素
%a%\n%b%
```
输出为：
```
["111"]
{"key2":"val2"}
```
### $JSON 包含 JSON名称 $
判断指定JSON数组/对象中是否包含某个元素。\
如果上述`参数`位置包含**一**个参数，则`JSON名称`处必须是一个**JSON数组**，函数将查询数组中是否存在该元素。\
如果上述`参数`位置包含**两**个参数，则`JSON名称`处必须是一个**JSON对象**， \
第一个参数指明你要查询**键**（填入`Key`）还是**值**（填入`Value`），第二个参数指明要查询的东西，函数就会在你指定的范围内寻找。\

如果元素被找到，则返回`t`，否则返回`f`。\

这个函数可以直接被放在`如果`语句的条件中，例如：
```NyaPlus
判断元素是否存在
a:["111","222"]
b:{"key1":"val1","key2":"val2"}
如果:$JSON 包含 a 222$
a数组有222\n
如果尾
如果:$JSON 包含 a 222$
a数组有222\n
如果尾
```

### $JSON 获取 JSON名称 参数$
从JSON数组/对象中获取包含某个元素。

此处`参数`位置只需一个参数，\
对于JSON数组，参数为要获取的元素的**索引**（即编号，从0开始）\
对于JSON对象，参数为要获取的元素的**键**。\
如果传入JSON数组，而后面传入的参数无法转化为一个整数，将会报错。

### $JSON 长度 JSON名称$
获取JSON数组/对象的长度。

### $跳 :标签$
跳转到指定标签位置。该函数需配合跳行标签`:标签名`来操作。\
需要注意的是，跳行标签仅支持跳转到同一代码块的标签。\
例如：
```NyaPlus
跳行测试
i:0
:start
如果:%i%<=10
%i%\s
i:[%i%+1]
$跳 :start$
如果尾
$跳 :end$
跳过的部分
:end
结束位置
```
输出为：
```
0 1 2 3 4 5 6 7 8 9 10 结束位置
```
从示例中可以看出，跳行函数支持向上跳转，也支持向下跳转。

### $jump :跳行标签$
与`$跳 :标签$`用法相同。

